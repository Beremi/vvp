{
 "cells": [
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 1. Paralelní výpočty v Cythonu (OpenMP)\n",
    "\n",
    "V této části navazujeme na minulou lekci a ukážeme si dvě praktické cesty, jak v Cythonu využít více vláken na CPU:\n",
    "\n",
    "- explicitní paralelní blok `with nogil, parallel():`\n",
    "- paralelní smyčku přes `prange()`"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 1.1 Paralelní blok `with nogil, parallel():`\n",
    "\n",
    "Jde o Cython obálku nad OpenMP direktivou pro paralelní sekci. Vlákna se vytvoří při vstupu do bloku a po jeho skončení se ukončí.\n",
    "\n",
    "Uvnitř bloku:\n",
    "- každé vlákno má vlastní kontext lokálních proměnných,\n",
    "- se sdílenými daty je potřeba pracovat opatrně,\n",
    "- identifikaci vlákna získáme přes `omp_get_thread_num()`."
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 1.2 Paralelní smyčka přes `prange()`\n",
    "\n",
    "`prange()` je paralelní alternativa k `range()`. Rozdělení iterací mezi vlákna řídí OpenMP.\n",
    "\n",
    "Důležité argumenty:\n",
    "- `num_threads`: počet vláken,\n",
    "- `schedule`: strategie rozdělení práce,\n",
    "- `chunksize`: velikost bloku iterací,\n",
    "- `nogil=True`: běh bez GIL.\n",
    "\n",
    "Operátor `+=` uvnitř `prange` znamená redukci do sdíleného výsledku. To je užitečné, ale je potřeba s tím počítat při návrhu algoritmu."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# pro jednoduchost použijeme Cython magic přímo v notebooku\n",
    "%load_ext cython"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 1.3 Příklad: norma vektoru"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### 1.3.1 Sekvenční verze"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cython --compile-args=-O3\n",
    "import numpy as np\n",
    "cimport numpy as np\n",
    "from libc.math cimport sqrt\n",
    "import cython\n",
    "\n",
    "@cython.boundscheck(False)\n",
    "@cython.wraparound(False)\n",
    "cpdef double my_norm_serial(np.ndarray[np.float64_t, ndim=1] a):\n",
    "    cdef int i\n",
    "    cdef int n = a.shape[0]\n",
    "    cdef double result = 0.0\n",
    "    for i in range(n):\n",
    "        result += a[i] * a[i]\n",
    "    return sqrt(result)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import numpy as np\n",
    "x = np.random.rand(4_000_000)\n",
    "y1 = my_norm_serial(x)\n",
    "y2 = np.linalg.norm(x)\n",
    "print(y1, y2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%timeit _ = np.linalg.norm(x)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%timeit _ = my_norm_serial(x)"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### 1.3.2 Paralelizace přes `with nogil, parallel():`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cython --compile-args=-fopenmp --compile-args=-O3 --link-args=-fopenmp\n",
    "\n",
    "import numpy as np\n",
    "cimport numpy as np\n",
    "from libc.math cimport sqrt\n",
    "import cython\n",
    "\n",
    "from cython.parallel import parallel\n",
    "from openmp cimport omp_get_thread_num\n",
    "\n",
    "ctypedef np.float64_t DTYPE_t\n",
    "\n",
    "@cython.boundscheck(False)\n",
    "@cython.wraparound(False)\n",
    "cpdef double my_norm_parallel_block(np.ndarray[np.float64_t, ndim=1] a, int num_threads):\n",
    "    cdef int i\n",
    "    cdef int n = a.shape[0]\n",
    "    cdef int chunk_size = n // num_threads\n",
    "    cdef int thread_num, start_idx, end_idx\n",
    "    cdef double local_sum\n",
    "    cdef double result = 0.0\n",
    "    cdef np.ndarray[np.float64_t, ndim=1] partial_sums = np.zeros((num_threads), dtype=np.float64)\n",
    "\n",
    "    with nogil, parallel(num_threads=num_threads):\n",
    "        thread_num = omp_get_thread_num()\n",
    "        start_idx = thread_num * chunk_size\n",
    "        if thread_num == num_threads - 1:\n",
    "            end_idx = n\n",
    "        else:\n",
    "            end_idx = (thread_num + 1) * chunk_size\n",
    "\n",
    "        local_sum = 0.0\n",
    "        for i in range(start_idx, end_idx):\n",
    "            local_sum = local_sum + a[i] * a[i]\n",
    "        partial_sums[thread_num] = local_sum\n",
    "\n",
    "    for i in range(num_threads):\n",
    "        result += partial_sums[i]\n",
    "\n",
    "    return sqrt(result)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "x = np.random.rand(4_000_000)\n",
    "y1 = my_norm_parallel_block(x, 4)\n",
    "y2 = np.linalg.norm(x)\n",
    "print(y1, y2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%timeit _ = my_norm_parallel_block(x, 8)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%timeit _ = np.linalg.norm(x)"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### 1.3.3 Paralelizace přes `prange()`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cython --compile-args=-fopenmp --compile-args=-O3 --link-args=-fopenmp\n",
    "\n",
    "import numpy as np\n",
    "cimport numpy as np\n",
    "from libc.math cimport sqrt\n",
    "import cython\n",
    "from cython.parallel import prange\n",
    "\n",
    "@cython.boundscheck(False)\n",
    "@cython.wraparound(False)\n",
    "cpdef double my_norm_parallel_prange(np.ndarray[np.float64_t, ndim=1] a, int num_threads):\n",
    "    cdef int i\n",
    "    cdef int n = a.shape[0]\n",
    "    cdef double result = 0.0\n",
    "    for i in prange(n, nogil=True, num_threads=num_threads):\n",
    "        result += a[i] * a[i]\n",
    "    return sqrt(result)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "x = np.random.rand(4_000_000)\n",
    "y1 = my_norm_parallel_prange(x, 4)\n",
    "y2 = np.linalg.norm(x)\n",
    "print(y1, y2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%timeit _ = my_norm_parallel_prange(x, 8)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%timeit _ = np.linalg.norm(x)"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 1.4 Srovnání rychlosti"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import os\n",
    "import time\n",
    "import matplotlib.pyplot as plt\n",
    "\n",
    "\n",
    "def measure_multi(n, func, data):\n",
    "    tmp_time = []\n",
    "    for _ in range(n):\n",
    "        start = time.time()\n",
    "        _ = func(data)\n",
    "        tmp_time.append(time.time() - start)\n",
    "    return min(tmp_time)\n",
    "\n",
    "n_loops = 5\n",
    "x = np.random.rand(2_000_000)\n",
    "\n",
    "max_threads = min(32, os.cpu_count() or 1)\n",
    "pocet_vlaken = []\n",
    "threads = 1\n",
    "while threads <= max_threads:\n",
    "    pocet_vlaken.append(threads)\n",
    "    threads *= 2\n",
    "\n",
    "time_numpy = measure_multi(n_loops, lambda data: np.linalg.norm(data), x)\n",
    "\n",
    "time_parallel_block = []\n",
    "time_parallel_prange = []\n",
    "\n",
    "for n_threads in pocet_vlaken:\n",
    "    time_parallel_block.append(\n",
    "        measure_multi(n_loops, lambda data: my_norm_parallel_block(data, n_threads), x)\n",
    "    )\n",
    "    time_parallel_prange.append(\n",
    "        measure_multi(n_loops, lambda data: my_norm_parallel_prange(data, n_threads), x)\n",
    "    )\n",
    "\n",
    "    print(\n",
    "        f\"vlaken: {n_threads}, with parallel(): {time_parallel_block[-1]:.6f}s, \"\n",
    "        f\"prange: {time_parallel_prange[-1]:.6f}s\"\n",
    "    )\n",
    "\n",
    "plt.loglog(pocet_vlaken, [time_numpy for _ in pocet_vlaken], label=\"numpy\")\n",
    "plt.loglog(pocet_vlaken, time_parallel_block, label=\"with parallel()\")\n",
    "plt.loglog(pocet_vlaken, time_parallel_prange, label=\"prange()\")\n",
    "plt.xlabel(\"Počet vláken\")\n",
    "plt.ylabel(\"Čas [s]\")\n",
    "plt.grid()\n",
    "plt.legend()"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": ".venv",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.14.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
